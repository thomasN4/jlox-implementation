// Singly linked list node
class Node {
    init(elt) {
        this.elt = elt;    // content
        this.next = nil;  // next Node
    }
}

// Singly linked list
class List {
    init(head) {
        this.head = head;
        this.tail = this.findTail();
    }

    findTail() {
        if (this.head == nil)
            return nil;
        var curr = this.head;
        while (curr.next != nil)
            curr = curr.next;
        return curr;
    }

    insert(new,prev) {  // new.next is assumed to be nil
        if (prev == this.tail) {
            this.push(new);
            return;
        }
        new.next = prev.next;
        prev.next = new;
        if (new.next == this.tail.next)
            this.tail = new;
    }

    insertList(list,prev) {
        if (list.head == nil)
            return;
        if (this.head == nil)
            this.init(list.head);
        list.tail.next = prev.next;
        prev.next = list.head;
        this.tail = findTail();
    }

    delete(node) {
        if (node == this.head) {
            this.head.next = nil;
            this.head = node.next;
        } else if (this.head != nil) {
            var curr = this.head;
            var done = false;
            while (!done and curr != this.tail) {
                if (curr.next == node) {
                    curr.next = node.next;
                    node.next = nil;
                    if (node == this.tail)
                        this.tail = curr;
                    done = true;
                }
                curr = curr.next;
            }
        }
    }

    push(node) {
        if (this.head == nil) {
            this.init(node);
            return;
        }
        this.tail.next = node;
        this.tail = node;
    }

    pop() {
        if (this.head == nil)
            return nil;
        var tail0 = this.tail;
        this.delete(this.tail);
        return tail0;
    }

    toString() {        
        if (this.head == nil)
            return "";
        var text = "";
        var curr = this.head;
        for (;curr != this.tail;) {
            text = text + curr.elt + "-";
            curr = curr.next;
        }
        text = text + curr.elt;
        return text;
    }
}

// Circular singly linked lists
class CList < List {
    init(head) {
        super.init(head);
        this.tail.next = this.head;
    }

    findTail() {
        if (this.head == nil)
            return nil;
        var curr = this.head;
        while (curr.next != this.head and curr.next != nil)
            curr = curr.next;
        return curr;
    }

    push(node) {
        super.push(node);
        node.next = this.head;
    }

    pop() {
        var tail0 = super.pop();
        if (this.tail != nil)
            this.tail.next = this.head;
        return tail0;
    }

    delete(node) {
        if (this.head == this.tail) {
            this.head = nil;
            this.tail = nil;
            return;
        }
        super.delete(node);
        if (node == this.head or node == this.tail)
            this.tail.next = this.head;
    }
}

fun main() {
    var nodeA = Node("apple");
    var nodeB = Node("banana");
    var nodeC = Node("cherry");
    var nodeD = Node("durian");
    var nodeE = Node("elderberry");
    var nodeF = Node("fig");
    var nodeG = Node("guava");
    var list = CList(nodeA);
    list.push(nodeB);
    list.push(nodeC);
    list.push(nodeE);
    list.delete(nodeB);
    list.insert(nodeG, nodeC);  // apple-cherry-guava-elderberry

    while (list.head != nil) {
        print list.toString();
        list.pop();
    }
    print list.toString();
}

main();
